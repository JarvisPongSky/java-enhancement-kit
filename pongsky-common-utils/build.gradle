plugins {
    id "signing"
    id "java-library"
    id "maven-publish"
}

task sourcesJar(type: Jar) {
    from sourceSets.main.allJava
    classifier 'sources'
}

task javadocJar(type: Jar) {
    from javadoc
    classifier 'javadoc'
}

publishing {
    repositories {
        maven {
            url "https://s01.oss.sonatype.org/service/local/staging/deploy/maven2"
            credentials {
                username ""
                password ""
            }
        }
    }
    publications {
        mavenJava(MavenPublication) {
            from components.java
            artifact sourcesJar
            artifact javadocJar

            groupId group.toString()
            artifactId "common-utils"
            version version.toString()
            description "Spring Cloud 增强工具 之 common-utils 模块"

            pom {
                name = "spring-cloud-enhancement-kit"
                description = "Spring Cloud 增强工具"
                url = "https://github.com/JarvisPongSky/spring-cloud-enhancement-kit"
                licenses {
                    license {
                        name = "GPL-v3.0 协议"
                        url = "https://www.gnu.org/licenses/gpl-3.0-standalone.html"
                    }
                }
                developers {
                    developer {
                        id = "PONGSKY"
                        name = "PONGSKY"
                        email = "kelry@vip.qq.com"
                    }
                }
                scm {
                    url = "https://github.com/JarvisPongSky/spring-cloud-enhancement-kit"
                }
            }
        }
    }
}

signing {
    sign publishing.publications.mavenJava
}

dependencies {
    compileOnly "org.projectlombok:lombok:$lombokVersion"
    annotationProcessor "org.projectlombok:lombok:$lombokVersion"

    compileOnly("io.minio:minio:$minioVersion") {
        exclude group: "com.squareup.okhttp3", module: "okhttp"
    }
    api "com.auth0:java-jwt:$jwtVersion"
    api "com.alibaba:fastjson:$fastjsonVersion"
    api "commons-codec:commons-codec:$commonsCodecVersion"
    api "org.apache.commons:commons-text:$commonsTextVersion"
    compileOnly "com.squareup.okhttp3:okhttp:$okHttpVersion"
    api "org.apache.commons:commons-lang3:$commonsLang3Version"
    api "io.springfox:springfox-boot-starter:$springfoxVersion"
    compileOnly "javax.servlet:javax.servlet-api:$servletVersion"
    compileOnly "com.aliyun.oss:aliyun-sdk-oss:$aliyunSdkOssVersion"
    api "org.apache.commons:commons-collections4:$commonsCollections4Version"
    compileOnly "org.hibernate.validator:hibernate-validator:$hibernateValidatorVersion"
}