import org.apache.tools.ant.filters.ReplaceTokens

plugins {
    id "signing"
    id "java-library"
    id "maven-publish"
    id "org.springframework.boot" version "$springBootVersion"
    id "io.spring.dependency-management" version "$springDependencyManagementVersion"
}

bootJar {
    enabled = false
}

dependencyManagement {
    imports {
        mavenBom "org.springframework.boot:spring-boot-dependencies:$springBootVersion"
        mavenBom "org.springframework.cloud:spring-cloud-dependencies:$springCloudVersion"
    }
}

compileJava.inputs.files(processResources)

processResources {
    filter ReplaceTokens, tokens: [name: rootProject.name.toString(), version: version.toString()]
}

task sourcesJar(type: Jar) {
    from sourceSets.main.allJava
    classifier 'sources'
}

task javadocJar(type: Jar) {
    from javadoc
    classifier 'javadoc'
}

publishing {
    repositories {
        maven {
            url "https://s01.oss.sonatype.org/service/local/staging/deploy/maven2"
            credentials {
                username ""
                password ""
            }
        }
    }
    publications {
        mavenJava(MavenPublication) {
            from components.java
            artifact sourcesJar
            artifact javadocJar

            groupId group.toString()
            artifactId "common-config-core"
            version version.toString()
            description "Spring Cloud 增强工具 之 config-core 模块"

            pom {
                name = "spring-cloud-enhancement-kit"
                description = "Spring Cloud 增强工具"
                url = "https://github.com/JarvisPongSky/spring-cloud-enhancement-kit"
                licenses {
                    license {
                        name = "GPL-v3.0 协议"
                        url = "https://www.gnu.org/licenses/gpl-3.0-standalone.html"
                    }
                }
                developers {
                    developer {
                        id = "PONGSKY"
                        name = "PONGSKY"
                        email = "kelry@vip.qq.com"
                    }
                }
                scm {
                    url = "https://github.com/JarvisPongSky/spring-cloud-enhancement-kit"
                }
            }
        }
    }
}

signing {
    sign publishing.publications.mavenJava
}

dependencies {
    api project(":common-utils")

    compileOnly "org.projectlombok:lombok:$lombokVersion"
    annotationProcessor "org.projectlombok:lombok:$lombokVersion"

    runtimeOnly "com.zaxxer:HikariCP:$hikariVersion"
    runtimeOnly "mysql:mysql-connector-java:$mysqlVersion"

    api "com.github.ben-manes.caffeine:caffeine:$caffeineVersion"
    api "com.google.code.findbugs:annotations:$googleAnnotationsVersion"
    compileOnly "com.baomidou:mybatis-plus-boot-starter:$mybatisPlusVersion"

    api "org.springframework.boot:spring-boot-starter-cache"
    api "org.springframework.boot:spring-boot-starter-actuator"
    api "org.springframework.boot:spring-boot-starter-validation"
    api "org.springframework.boot:spring-boot-starter-data-redis"
    compileOnly "org.springframework.boot:spring-boot-starter-web"
    compileOnly "org.springframework.boot:spring-boot-starter-quartz"
    annotationProcessor "org.springframework.boot:spring-boot-configuration-processor"

    api "org.springframework.cloud:spring-cloud-starter-bootstrap"
    api "org.springframework.cloud:spring-cloud-starter-openfeign"
    api "org.springframework.cloud:spring-cloud-starter-consul-discovery"
}