import org.apache.tools.ant.filters.ReplaceTokens

plugins {
    id "signing"
    id "java-library"
    id "maven-publish"
    id "org.springframework.boot" version "$springBootVersion"
    id "io.spring.dependency-management" version "$springDependencyManagementVersion"
}

jar {
    enabled = true
}

bootJar {
    enabled = false
}

dependencyManagement {
    imports {
        mavenBom "org.springframework.boot:spring-boot-dependencies:$springBootVersion"
        mavenBom "org.springframework.cloud:spring-cloud-dependencies:$springCloudVersion"
    }
}

compileJava.inputs.files(processResources)

processResources {
    filter ReplaceTokens, tokens: [name: rootProject.name.toString(), version: version.toString()]
}

task sourcesJar(type: Jar) {
    from sourceSets.main.allJava
    classifier 'sources'
}

task javadocJar(type: Jar) {
    from javadoc
    classifier 'javadoc'
}

publishing {
    repositories {
        maven {
            url "https://s01.oss.sonatype.org/service/local/staging/deploy/maven2"
            credentials {
                username ""
                password ""
            }
        }
    }
    publications {
        mavenJava(MavenPublication) {
            from components.java
            artifact sourcesJar
            artifact javadocJar

            groupId group.toString()
            artifactId "config-core"
            version version.toString()
            description "Java 增强套件 之 config-core 模块"

            pom {
                name = "java-enhancement-kit"
                description = "Java 增强套件 之 config-core 模块"
                url = "https://github.com/JarvisPongSky/java-enhancement-kit"
                licenses {
                    license {
                        name = "GPL-v3.0 协议"
                        url = "https://www.gnu.org/licenses/gpl-3.0-standalone.html"
                    }
                }
                developers {
                    developer {
                        id = "PONGSKY"
                        name = "PONGSKY"
                        email = "kelry@vip.qq.com"
                    }
                }
                scm {
                    url = "https://github.com/JarvisPongSky/java-enhancement-kit"
                }
            }
        }
    }
}

signing {
    sign publishing.publications.mavenJava
}

dependencies {
    api project(":common-utils")

    compileOnly "org.projectlombok:lombok:$lombokVersion"
    annotationProcessor "org.projectlombok:lombok:$lombokVersion"

    compileOnly "com.alibaba:druid-spring-boot-starter:$druidVersion"
    compileOnly "com.baomidou:mybatis-plus-boot-starter:$mybatisPlusVersion"
    compileOnly "com.google.code.findbugs:annotations:$googleAnnotationsVersion"

    compileOnly "org.springframework.boot:spring-boot-starter-web"
    compileOnly "org.springframework.boot:spring-boot-starter-quartz"
    compileOnly "org.springframework.boot:spring-boot-starter-security"
    compileOnly "org.springframework.boot:spring-boot-starter-validation"
    compileOnly "org.springframework.boot:spring-boot-starter-data-redis"
    annotationProcessor "org.springframework.boot:spring-boot-configuration-processor"

    compileOnly "org.springframework.cloud:spring-cloud-starter-openfeign"
}